properties([
  parameters([
    [$class: 'ChoiceParameter',
      choiceType: 'SINGLE_SELECT',
      description: 'Pick version number to deploy',
      name: 'DOCKER_IMAGE',
      script: [$class: 'GroovyScript',
        fallbackScript: [classpath: [], sandbox: false, script: 'return ["ERROR"]'],
        script: [classpath: [], sandbox: false, 
          script: """
            import groovy.json.JsonSlurper
            import jenkins.model.*
            
            def registry_url = "docker-registry.project.com"
            def image = "spring-petclinic"
            def registry_credentials = com.cloudbees.plugins.credentials.CredentialsProvider.lookupCredentials(
              com.cloudbees.plugins.credentials.impl.UsernamePasswordCredentialsImpl, Jenkins.instance, null, null ).find{
                it.id == "registry-credentials"}
            
            def command = \"curl -s -u \$registry_credentials.username:\$registry_credentials.password https://\$registry_url/v2/\$image/tags/list\"
            def proc = command.execute()
            proc.waitFor()              
            def curl_output = proc.in.text
            
            def jsonSlurper = new JsonSlurper()
            def json_output = jsonSlurper.parseText(curl_output)
            def tags_list = []
            
            for(tag in json_output['tags']) {
              def full_image = \"\$registry_url/\$image:\$tag\"
              if(tag == "latest") {
                full_image += ":selected"
              }
              tags_list.push(full_image)
            }
            
            return tags_list
          """.stripIndent().trim()
        ]]]
  ])
])

pipeline {
  agent {
    kubernetes {
      yaml '''
        apiVersion: v1
        kind: Pod
        spec:
          containers:
          - name: gcloud
            image: google/cloud-sdk:385.0.0
            command:
            - sleep
            args:
            - 99d
            volumeMounts:
            - name: kubeconfig
              mountPath: /home/jenkins/agent/.kube
          - name: helm
            image: alpine/helm:3.8.2
            command:
            - sleep
            args:
            - 99d
            volumeMounts:
            - name: kubeconfig
              mountPath: /root/.kube
          volumes:
          - name: kubeconfig
            emptyDir: {}
      '''
    }
  }

  parameters {
    choice(name: 'ENV_NAME', choices: ['CI', 'QA'], description: 'Pick env name')
  }

  stages {
    stage('Login with service account') {
      steps {
        withCredentials([file(credentialsId: 'gke-service-account', variable: 'SA_KEY')]) {
          container('gcloud') {
            sh 'gcloud auth activate-service-account --key-file=$SA_KEY'
          }
        }
      }
    }

    stage('Create Helm Values file for CI env') {
      when {
        expression { ENV_NAME == 'CI' }
      }
      steps {
        withCredentials([
          string(credentialsId: 'mysql-database', variable: 'MYSQL_DATABASE'),
          string(credentialsId: 'mysql-username', variable: 'MYSQL_USERNAME'),
          string(credentialsId: 'mysql-password', variable: 'MYSQL_PASSWORD'),
          string(credentialsId: 'mysql-connection-name', variable: 'MYSQL_CONNECTION'),
          string(credentialsId: 'mysql-service-account', variable: 'MYSQL_SA')
          ]) {
            sh '''cat <<EOF > ci-values.yaml
            image: $DOCKER_IMAGE
            mysql:
              enabled: true
              database: $MYSQL_DATABASE
              username: $MYSQL_USERNAME
              password: $MYSQL_PASSWORD
              connectionName: $MYSQL_CONNECTION
              serviceAccount: $MYSQL_SA
            loadBalancer:
              type: external
              addressName: spring-petclinic-static-ip
              domains:
              - spring-petclinic.tk
            EOF'''.stripIndent()
        }
      }
    }

    stage('Get CI cluster credentials') {
      when {
        expression { ENV_NAME == 'CI' }
      }
      steps {
        withCredentials([
          string(credentialsId: 'ci-cluster-name', variable: 'CI_CLUSTER_NAME'),
          string(credentialsId: 'cluster-location', variable: 'LOCATION'),
          string(credentialsId: 'project-id', variable: 'PROJECT_ID')
          ]) {
          container('gcloud') {
            sh 'gcloud container clusters get-credentials $CI_CLUSTER_NAME --zone $LOCATION --project $PROJECT_ID'
          }        
        }
      }
    }

    stage('Install Helm Release on CI env') {
      when {
        expression { ENV_NAME == 'CI' }
      }
      steps {
          container('helm') {
            sh 'helm upgrade --install spring-petclinic ./spring-petclinic-chart -f ci-values.yaml  --namespace petclinic --create-namespace'
          }
      }
    }

    stage('Get QA cluster credentials') {
      when {
        expression { ENV_NAME == 'QA' }
      }
      steps {
        withCredentials([
          string(credentialsId: 'qa-cluster-name', variable: 'QA_CLUSTER_NAME'),
          string(credentialsId: 'cluster-location', variable: 'LOCATION'),
          string(credentialsId: 'project-id', variable: 'PROJECT_ID')
          ]) {
          container('gcloud') {
            sh 'gcloud container clusters get-credentials $QA_CLUSTER_NAME --region $LOCATION --project $PROJECT_ID'
          }
        }
      }
    }

    stage('Install Helm release on QA env') {
      when {
        expression { ENV_NAME == 'QA' }
      }
      steps {
        container('helm') {
          sh 'helm upgrade --install spring-petclinic ./spring-petclinic-chart --set image=$DOCKER_IMAGE --namespace petclinic --create-namespace'
        }
      }
    }
  }
}
